protocol PlainCodec;

def PlainCodec.new(type) -> new PlainCodec {
  type := type
}

def (this is PlainCodec).is_atomic -> false;

def (this is PlainCodec).new_instance()
 -> this.type.new_instance();

def (this is PlainCodec).initialize(obj, payload)
 -> obj.initialize(payload);

protocol GetterCodec;

def GetterCodec.new(name) -> new GetterCodec {
  name := name
}

def (this is GetterCodec).is_atomic -> true;

def (this is GetterCodec).new_atomic_instance(payload)
 -> payload[this.name];

protocol AtomicCodec;

def AtomicCodec.new(type) -> new AtomicCodec {
  type := type
}

def (this is AtomicCodec).is_atomic -> true;

def (this is AtomicCodec).new_atomic_instance(payload)
 -> this.type.new_instance(payload);

protocol DummyCodec {

  def this.is_atomic -> true;

  def this.new_atomic_instance(payload) -> this.value;

}

def DummyCodec.new(value) -> new DummyCodec { value := value };

def CompilerGrower.new_handler_table() {
  def result := new HashMap();
  result[CodeBundle.tag] := new PlainCodec(CodeBundle);
  result[Parameter.tag] := new PlainCodec(Parameter);
  result[TypeId.tag] := new AtomicCodec(TypeId);
  result[Binding.tag] := new PlainCodec(Binding);
  result[Annotation.tag] := new PlainCodec(Annotation);
  result[Module.tag] := new PlainCodec(Module);
  result[Native.tag] := new PlainCodec(Native);
  result[Universe.tag] := new PlainCodec(Universe);
  result[ProtocolAst.tag] := new PlainCodec(ProtocolAst);
  result[MethodAst.tag] := new PlainCodec(MethodAst);
  result[FieldKey.tag] := new PlainCodec(FieldKey);
  result["org.javatrino.ast.Expression$Call$Argument"] := new PlainCodec(CallArgument);
  result["org.javatrino.ast.Symbol"] := new PlainCodec(SymbolToken);
  result["org.javatrino.ast.Expression$GetField"] := new DummyCodec("get_field");
  result["org.javatrino.ast.Expression$SetField"] := new DummyCodec("set_field");
  result["org.javatrino.ast.Expression$Block"] := new PlainCodec(BlockExpr);
  result["org.javatrino.ast.Expression$NewObject"] := new PlainCodec(NewObject);
  result["org.javatrino.ast.Expression$NewArray"] := new DummyCodec("new_array");
  result["org.javatrino.ast.Expression$TagWithProtocol"] := new DummyCodec("tag_with_protocol");
  result["org.javatrino.ast.Expression$AddIntrinsics"] := new PlainCodec(AddIntrinsics);
  result["org.javatrino.ast.Expression$WithEscape"] := new DummyCodec("with_escape");
  result["org.javatrino.ast.Expression$Call"] := new PlainCodec(CallExpr);
  result["org.javatrino.ast.Expression$Constant"] := new PlainCodec(ConstantExpr);
  result["org.javatrino.ast.Expression$Local"] := new PlainCodec(LocalExpr);
  result["org.javatrino.ast.Expression$Global"] := new PlainCodec(GlobalExpr);
  result["org.javatrino.ast.Expression$Definition"] := new PlainCodec(DefExpr);
  result["org.javatrino.ast.Method"] := new PlainCodec(NewMethod);
  result["org.javatrino.ast.Pattern"] := new PlainCodec(Pattern);
  result["org.javatrino.ast.Test$Eq"] := new PlainCodec(EqTest);
  result["org.javatrino.ast.Test$Is"] := new DummyCodec("is");
  result["org.javatrino.ast.Test$Any"] := new DummyCodec("?");
  result["org.javatrino.ast.Expression$Internal"] := new DummyCodec("i");
  result["org.neutrino.runtime.RInteger"] := new GetterCodec("value");
  result["org.neutrino.runtime.RString"] := new GetterCodec("value");
  result;
}

protocol SymbolToken {

  def this.initialize(payload);

}

def SymbolToken.new_instance() -> new SymbolToken { }

protocol EqTest {

  def this.initialize(payload) {
    this.value := payload["value"];
  }

}

def EqTest.new_instance() -> new EqTest {
  value := null
}

protocol NewMethod {

  def this.initialize(payload) {
    this.signature := payload["signature"];
    this.accepts_extra_arguments := payload["acceptsExtraArguments"];
    this.body := payload["body"];
    this.rewrites := payload["rewrites"];
  }

}

def NewMethod.new_instance() -> new NewMethod {
  signature := null,
  accepts_extra_arguments := null,
  body := null,
  rewrites := null,
  has_been_analyzed := false
}

def (this is NewMethod).ensure_analyzed(universe) {
  if not this.has_been_analyzed then {
    new TypeAnalysis(universe, this).analyze(this.body);
    this.has_been_analyzed := true;
  }
}


protocol Expr {

  def this.info {
    if this.info_cache = null
      then this.info_cache := new NodeInfo();
    this.info_cache;
  }

  def this.for(fun);

}

protocol NewObject is Expr {

  def this.initialize(payload);

}

def NewObject.new_instance() -> new NewObject {
  info_cache := null
}

protocol AddIntrinsics is Expr {

  def this.initialize(payload) {
    this.object := payload["object"];
    this.methods := payload["methods"];
  }
  
  def this.for(fun) -> fun(this.object);

}

def AddIntrinsics.new_instance() -> new AddIntrinsics {
  object := null,
  methods := null,
  info_cache := null
}

protocol CallArgument is Expr {

  def this.initialize(payload) {
    this.tag := payload["tag"];
    this.value := payload["value"];
  }
  
  def this.to_string() -> "${this.tag}: ${this.value}";

}

def CallArgument.new_instance() -> new CallArgument {
  tag := null,
  value := null,
  info_cache := null
}

protocol Pattern {

  def this.initialize(payload) {
    this.tags := payload["tags"];
    this.test := payload["test"];
    this.symbol := payload["symbol"];
  }
  
}

def Pattern.new_instance() -> new Pattern {
  tags := null,
  test := null,
  symbol := null
}

protocol ConstantExpr is Expr {

  def this.initialize(payload) {
    this.value := payload["value"];
  }

}

def ConstantExpr.new_instance() -> new ConstantExpr {
  value := null,
  info_cache := null
}

protocol CallExpr is Expr {

  def this.initialize(payload) {
    def raw_args := payload["arguments"];
    def args := new ArrayList();
    for (i : 1 .. raw_args.length)
      args.add(raw_args[i].value);
    this.arguments := args;
    this.name := raw_args[0].value.value;
  }
  
  def this.for(fun) -> this.arguments.for(fun);

  def this.to_string() -> "${this.name}(${this.arguments})";

}

def CallExpr.new_instance() -> new CallExpr {
  arguments := null,
  name := null,
  info_cache := null
}

protocol GlobalExpr is Expr {

  def this.initialize(payload) {
    this.name := payload["name"];
  }

}

def GlobalExpr.new_instance() -> new GlobalExpr {
  name := null,
  info_cache := null
}

protocol BlockExpr is Expr {

  def this.initialize(payload) {
    this.values := payload["values"];
  }
  
  def this.for(fun) -> this.values.for(fun);
  
  def this.to_string() -> "{${this.values}}";

}

def BlockExpr.new_instance() -> new BlockExpr {
  values := null,
  info_cache := null
}

protocol LocalExpr is Expr {

  def this.initialize(payload) {
    this.symbol := payload["symbol"];
  }
  
  def this.to_string() -> "#";

}

def LocalExpr.new_instance() -> new LocalExpr {
  symbol := null,
  info_cache := null
}

protocol DefExpr is Expr {

  def this.initialize(payload) {
    this.symbol := payload["symbol"];
    this.value := payload["value"];
    this.body := payload["body"];
  }
  
  def this.for(fun) {
    fun(this.value);
    fun(this.body);
  }
  
  def this.to_string() -> "def ${this.symbol} := ${this.value} in ${this.body}";

}

def DefExpr.new_instance() -> new DefExpr {
  symbol := null,
  value := null,
  body := null,
  info_cache := null
}

protocol CodeBundle;

def CodeBundle.tag -> "org.neutrino.pib.CodeBundle";

def CodeBundle.new_instance() -> new CodeBundle {
  literals := null,
  local_count := null,
  filename := null,
  code := null,
  root_offset := null,
  body := null
}

def (this is CodeBundle).initialize(payload) {
  this.literals := payload["oldLiterals"];
  this.local_count := payload["oldLocalCount"];
  this.filename := payload["fileName"];
  this.code := payload["oldCode"];
  this.root_offset := payload["rootOffset"];
  this.body := payload["body"];
}

protocol Parameter;

def Parameter.tag -> "org.neutrino.pib.Parameter";

def Parameter.new_instance() -> new Parameter {
  type := null,
  name := null,
  is_protocol := null,
  symbol := null
}

def (this is Parameter).initialize(payload) {
  this.type := payload["type"];
  this.name := payload["name"];
  this.symbol := payload["symbol"];
  this.is_protocol := payload["isProtocolMethod"];
}

protocol MethodAst;

def MethodAst.tag -> "org.neutrino.runtime.RMethod";

def MethodAst.new_instance() -> new MethodAst {
  annots := null,
  params := null,
  name := null,
  code := null,
  syntax_tree := null,
  has_been_analyzed := false
}

def (this is MethodAst).initialize(payload) {
  this.annots := payload["annots"];
  this.params := payload["params"];
  this.name := payload["name"];
  this.code := payload["code"];
}

def (this is MethodAst).body -> this.code.body;

def (this is MethodAst).get_annotation(name) {
  with_escape (yield) {
    for (annot : this.annots) {
      if annot.tag = name
        then yield(annot);
    }
    null;
  }
}

def (this is MethodAst).ensure_analyzed(universe) {
  if not this.has_been_analyzed then {
    new TypeAnalysis(universe, this).analyze(this.body);
    this.has_been_analyzed := true;
  }
}

def (this is MethodAst).to_string() -> "#<a Method: ${this.name}>";

protocol TypeId;

def TypeId.tag -> "org.neutrino.runtime.TypeId";

def TypeId.new_instance(payload) -> null;

protocol ProtocolAst;

def ProtocolAst.tag -> "org.neutrino.runtime.RProtocol";

def ProtocolAst.new_instance() -> new ProtocolAst {
  instance_type_id := null,
  protocol_type_id := null,
  id := null,
  annotations := null,
  display_name := null
}

def (this is ProtocolAst).initialize(payload) {
  this.instance_type_id := payload["instanceTypeId"];
  this.protocol_type_id := payload["protocolTypeId"];
  this.id := payload["id"];
  this.annotations := payload["annotations"];
  this.display_name := payload["displayName"];
}

def (this is ProtocolAst).get_annotation(name) {
  with_escape (yield) {
    for (annot : this.annotations) {
      if annot.tag = name
        then yield(annot);
    }
    null;
  }
}

protocol Binding;

def Binding.tag -> "org.neutrino.pib.Binding";

def Binding.new_instance() -> new Binding {
  annotations := null,
  code := null
}

def (this is Binding).initialize(payload) {
  this.annotations := payload["annotations"];
  this.code := payload["code"];
}

def (this is Binding).get_annotation(name) {
  with_escape (yield) {
    for (annot : this.annotations) {
      if annot.tag = name
        then yield(annot);
    }
    null;
  }
}

protocol Annotation;

def Annotation.tag -> "org.neutrino.syntax.Annotation";

def Annotation.new_instance() -> new Annotation {
  tag := null,
  args := null
}

def (this is Annotation).initialize(payload) {
  this.tag := payload["tag"];
  this.args := payload["args"];
}

def (this is Annotation)[index] -> this.args[index];

protocol Module;

def Module.tag -> "org.neutrino.pib.Module";

def Module.new_instance() -> new Module {
  defs := null,
  protos := null,
  supers := null,
  methods := null
}

def (this is Module).initialize(payload) {
  this.defs := payload["defs"];
  this.protos := payload["protos"];
  this.supers := payload["rawInheritance"];
  this.methods := payload["methods"];
}

def (this is Module).to_string()
 -> "#<a Module: defs=${this.defs}, protos=${this.protos}, supers=${this.supers}, methods=${this.methods}>";

protocol Native;

def Native.tag -> "org.neutrino.runtime.Native";

def Native.grow(payload) -> new Native {
  name := payload["name"]
}

protocol FieldKey;

def FieldKey.tag -> "org.neutrino.runtime.RFieldKey";

def FieldKey.new_instance() -> new FieldKey { }

def (this is FieldKey).initialize(payload) -> null;

protocol Universe;

def Universe.tag -> "org.neutrino.pib.Universe";

def Universe.new_instance() -> new Universe {
  modules := null,
  global_cache := new HashMap(),
  super_cache := new HashMap()
}

def (this is Universe).initialize(payload) {
  this.modules := payload["modules"];
}

def (this is Universe).modules -> this.r_modules.get();

def (this is Universe).bindings -> fn for (fun) {
  for (module : this.modules.values)
    module.defs.for(fun);
}

def (this is Universe).methods -> fn for (fun) {
  for (module : this.modules.values)
    module.methods.for(fun);
}

def (this is Universe).protocols -> fn for (fun) {
  for (module : this.modules.values)
    module.protos.for(fun);
}

def (this is Universe).supers -> fn for (fun) {
  for (module : this.modules.values)
    module.supers.for(fun);
}

def (this is Universe).supers(name) {
  def cached := this.super_cache[name];
  if cached = null then {
    def result := new ArrayList();
    for (found_name, found_supers : this.supers) {
      if found_name = name then {
        for (super : found_supers)
          result.add(super);
      }
    }
    this.super_cache[name] := result;
  } else {
    cached;
  }
}

def (this is Universe).lookup_static_global(name) {
  def cached := this.global_cache[name];
  if cached = null then {
    with_escape (return) {
      for (proto_name, proto : this.protocols) {
        if name = proto_name then {
          this.global_cache[name] := proto;
          return(proto);
        }
      }
      null;
    }
  } else {
    cached;
  }
}

def (this is Universe).to_string() -> "#<a Universe: ${this.modules}>";
