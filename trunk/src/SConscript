from os.path import join, abspath
from buildutils import read_files_from, apply_items
Import('config target properties prefix')

def get_wildcard_mapping():
  result = { }
  for (key, value) in config.items():
    result[key.lower()] = str(value).lower()
  for (key, value) in properties.items():
    result[key.lower()] = str(value).lower()
  return result

source_files = read_files_from(get_wildcard_mapping(), config['src'], join(config['src'], 'library.list'))
program_files = read_files_from(get_wildcard_mapping(), config['src'], join(config['src'], 'main', 'program.list'))

# Creates a new environment and pre-configure it based on the set
# of configuration items
def create_environment():
  result = Environment(CPPPATH=".")
  apply_items(result, properties)
  return result

env = create_environment()

static_library = env.StaticLibrary(config['name'], source_files)
shared_library = env.SharedLibrary(config['name'], source_files)
program = env.Program(config['name'], [ shared_library ] + program_files)
config[prefix + 'static-library'] = static_library[0]
config[prefix + 'shared-library'] = shared_library[0]
Alias(prefix + 'program', program)
config[prefix + 'program'] = program[0]
env.Alias(target, [shared_library, program])
