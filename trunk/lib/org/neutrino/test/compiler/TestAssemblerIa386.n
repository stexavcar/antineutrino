protocol Ia386Test;

def kHexChars := #[
  "0", "1", "2", "3", "4", "5", "6", "7",
  "8", "9", "a", "b", "c", "d", "e", "f"
];

def int8_to_hex(value) {
  def nib_1 := value & 16r0F;
  def nib_2 := (value >> 4) & 16r0F;
  "${kHexChars[nib_2]}${kHexChars[nib_1]}";
}

def int16_to_hex(value) {
  def byte_0 := int8_to_hex(value & 16rFF);
  def byte_1 := int8_to_hex((value >> 8) & 16rFF);
  "${byte_1}${byte_0}";
}

def int32_to_hex(value) {
  def byte_0 := int8_to_hex(value & 16rFF);
  def byte_1 := int8_to_hex((value >> 8) & 16rFF);
  def byte_2 := int8_to_hex((value >> 16) & 16rFF);
  def byte_3 := int8_to_hex((value >> 24) & 16rFF);
  "${byte_3}${byte_2}${byte_1}${byte_0}";
}

def to_hex32(value) -> "0x${int32_to_hex(value)}";

def to_hex16(value) -> "0x${int16_to_hex(value)}";

def to_hex8(value) -> "0x${int8_to_hex(value)}";

def (this is Memory).to_string() {
  if this.disp != 0 or this.base = rEbp then {
    "${to_hex8(this.disp)}(${this.base})";
  } else {
    "(${this.base})";
  }
}

def kRegisters := new ArrayList [
  rEax, rEbx, rEcx, rEdx, rEsi, rEdi, rEbp, rEsp
];

def kImmediates := new ArrayList [ 0-1, 0, 10, 5 << 28 ];

def kMakers := #[
  fn (reg) -> reg,
  fn (reg) -> Memory.at(reg),
  fn (reg) -> Memory.at_disp(reg, 4),
  fn (reg) -> Memory.at_disp(reg, 0-4)
];

protocol TestData;

def (this is TestData).add_expected(str) -> this.expected.add(str);

@entry_point("test_ia386")
def test_ia386(args) {
  def binary := new Binary(new Ia386());
  def test := new Ia386Test { };
  def datas := new ArrayList();
  def new_data(name) {
    def result := new TestData {
      name := name,
      assm := binary.add_code_block(name),
      expected := new ArrayList()
    }
    datas.add(result);
  }
  test.run_tests(new_data);
  
  def bin_name := args[0];
  open_file(bin_name).write_binary(binary.encode);
  
  def blocks := new ArrayList();
  for (data : datas)
    blocks.add("${data.name}:\n${data.expected.join("\n")}");
  def expected := blocks.join("\n");
  def exp_name := args[1];
  open_file(exp_name).write_string("${expected}\n");
}

def (this is Ia386Test).operands -> fn for (fun) {
  for (reg : this.registers) for (maker : kMakers) {
    fun(maker(reg));
  }
}

def (this is Ia386Test).operand_pairs -> fn for (fun) {
  for (src : this.operands) for (dest : this.operands) {
    if this.is_valid(src, dest)
      then fun(src, dest);
  }
}

def (this is Ia386Test).registers -> kRegisters;

def (this is Ia386Test).immediates -> kImmediates;

def (this is Ia386Test).is_valid(a, b) -> true;

def (this is Ia386Test).is_valid(a is Memory, b is Memory) -> false;

def (this is Ia386Test).run_tests(new_data) {
  // These should be first because their expectations encode absolute
  // addresses.
  this.jmp_forward(new_data("jmp_forward"));
  this.jmp_back(new_data("jmp_back"));
  this.branch(new_data("branch"));

  this.mov(new_data("mov"));
  this.mov_imm(new_data("mov_imm"));
  this.add(new_data("add"));
  this.add_imm(new_data("add_imm"));
  this.sub(new_data("sub"));
  this.sub_imm(new_data("sub_imm"));
  this.ahnd(new_data("and"));
  this.and_imm(new_data("and_imm"));
  this.push(new_data("push"));
  this.pop(new_data("pop"));
  this.simple(new_data("simple"));
  this.call(new_data("call"));
  this.syms(new_data("syms"));
  this.test(new_data("test"));
  this.test_imm(new_data("test_imm"));
}

def (this is Ia386Test).mov(data) {
  for (src, dest : this.operand_pairs) {
    data.assm.mov(dest, src);
    data.add_expected("movl ${src},${dest}");
  }
}

def (this is Ia386Test).add(data) {
  for (src, dest : this.operand_pairs) {
    data.assm.add(dest, src);
    data.add_expected("addl ${src},${dest}");
  }
}

def (this is Ia386Test).add_imm(data) {
  for (dest : this.operands) for (imm : this.immediates) {
    data.assm.add(dest, imm);
    data.add_expected("addl $${to_hex32(imm)},${dest}");
  }
}

def (this is Ia386Test).sub(data) {
  for (src, dest : this.operand_pairs) {
    data.assm.sub(dest, src);
    data.add_expected("subl ${src},${dest}");
  }
}

def (this is Ia386Test).sub_imm(data) {
  for (dest : this.operands) for (imm : this.immediates) {
    data.assm.sub(dest, imm);
    data.add_expected("subl $${to_hex32(imm)},${dest}");
  }
}

def (this is Ia386Test).ahnd(data) {
  for (src, dest : this.operand_pairs) {
    data.assm.ahnd(dest, src);
    data.add_expected("andl ${src},${dest}");
  }
}

def (this is Ia386Test).and_imm(data) {
  for (dest : this.operands) for (imm : this.immediates) {
    data.assm.ahnd(dest, imm);
    data.add_expected("andl $${to_hex32(imm)},${dest}");
  }
}

def (this is Ia386Test).mov_imm(data) {
  for (dest : this.operands) for (imm : this.immediates) {
    data.assm.mov(dest, imm);
    data.add_expected("movl $${to_hex32(imm)},${dest}");
  }
}

def (this is Ia386Test).push(data) {
  for (src : this.operands) {
    data.assm.push(src);
    data.add_expected("pushl ${src}");
  }
}

def (this is Ia386Test).pop(data) {
  for (dest : this.operands) {
    data.assm.pop(dest);
    data.add_expected("popl ${dest}");
  }  
}

def (this is Ia386Test).simple(data) {
  data.assm.int3();
  data.add_expected("int $0x3");
  data.assm.ret();
  data.add_expected("ret");
  data.assm.leave();
  data.add_expected("leave");
  data.assm.enter(18);
  data.add_expected("enter $${to_hex16(18)},$0x00");
}

def (this is Ia386Test).call_expectation(dest)
 -> "call *${dest}";

def (this is Ia386Test).call_expectation(dest is Memory)
 -> if dest.disp = 0 and dest.base != rEbp and dest.base != rEsp
      then "call ${dest}"
      else "call *${dest}";

def (this is Ia386Test).call(data) {
  for (dest : this.operands) {
    data.assm.call(dest);
    data.add_expected(this.call_expectation(dest));
  }  
}

def (this is Ia386Test).syms(data) {
  data.assm.call(Symbol.local("mov"));
  data.add_expected("calll mov");
}

def (this is Ia386Test).jmp_forward(data) {
  def label := data.assm.label();
  data.assm.jmp(label);
  data.add_expected("jmpl jmp_back");
  data.assm.mov(rEax, rEbx);
  data.add_expected("movl ${rEbx},${rEax}");
  data.assm.jmp(label);
  data.add_expected("jmpl jmp_back");
  data.assm.mov(rEax, rEbx);
  data.add_expected("movl ${rEbx},${rEax}");
  data.assm.jmp(label);
  data.add_expected("jmpl jmp_back");
  data.assm.mov(rEax, rEbx);
  data.add_expected("movl ${rEbx},${rEax}");
  data.assm.jmp(label);
  data.add_expected("jmpl jmp_back");
  data.assm.mov(rEax, rEbx);
  data.add_expected("movl ${rEbx},${rEax}");
  data.assm.bind(label);
}

def (this is Ia386Test).jmp_back(data) {
  def start := data.assm.here();
  data.assm.mov(rEax, rEbx);
  data.add_expected("movl ${rEbx},${rEax}");
  data.assm.jmp(start);
  data.add_expected("jmpl jmp_back");
  def mid := data.assm.here();
  data.assm.mov(rEax, rEbx);
  data.add_expected("movl ${rEbx},${rEax}");
  data.assm.jmp(start);
  data.add_expected("jmpl jmp_back");
  data.assm.jmp(mid);
  data.add_expected("jmpl 0x00000167");
}

def (this is Ia386Test).branch(data) {
  def label := data.assm.here();
  data.assm.ja(label);
  data.add_expected("jal branch");
  data.assm.jae(label);
  data.add_expected("jael branch");
  data.assm.jb(label);
  data.add_expected("jbl branch");
  data.assm.jbe(label);
  data.add_expected("jbel branch");
  data.assm.je(label);
  data.add_expected("jel branch");
  data.assm.jne(label);
  data.add_expected("jnel branch");
  data.assm.jg(label);
  data.add_expected("jgl branch");
  data.assm.jge(label);
  data.add_expected("jgel branch");
  data.assm.jl(label);
  data.add_expected("jll branch");
  data.assm.jle(label);
  data.add_expected("jlel branch");
}  

def (this is Ia386Test).test(data) {
  for (a : this.operands) for (b : this.registers) {
    data.assm.test(b, a);
    data.add_expected("testl ${a},${b}");
  }
}

def (this is Ia386Test).test_imm(data) {
  for (a : this.operands) for (b : this.immediates) {
    data.assm.test(a, b);
    data.add_expected("testl $${to_hex32(b)},${a}");
  }
}
