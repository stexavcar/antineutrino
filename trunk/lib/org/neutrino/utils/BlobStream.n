(*
 * A stream of bytes.
 *)
protocol BlobStream;

def BlobStream.new -> new BlobStream(256);

def BlobStream.new(size: Integer) -> new BlobStream {
  data_ := new_ref(new MutablePrimitiveByteArray(size)),
  length_ := new_ref(0)
};

def (this: BlobStream).length -> this.length_.get();

def (this: BlobStream).capacity -> this.data.length;

def (this: BlobStream).data -> this.data_.get();

def (this: BlobStream).write_int32(word) {
  this.add(word & 16rFF);
  this.add((word >> 8) & 16rFF);
  this.add((word >> 16) & 16rFF);
  this.add((word >> 24) & 16rFF);
}

def (this: BlobStream).write_data(data, from, to) {
  for (def i : from .. to)
    this.add(data[i]);
}

def (this: BlobStream).extend_capacity(capacity) {
  def old_contents := this.data;
  def new_contents := new MutablePrimitiveByteArray(capacity * 2);
  for (def i : 0 .. this.length)
    new_contents[i] := old_contents[i];
  this.data_.set(new_contents);
}

def (this: BlobStream).pad_to(offset) {
  if this.length < offset then {
    for (def i : this.length .. offset)
      this.add(0);
  }
}

def (this: BlobStream).add(byte) {
  if this.length >= this.capacity then
    this.extend_capacity(this.length);
  def index := this.length;
  this.data[index] := byte;
  this.length_.set(index + 1);
}

def (this: BlobStream).get_blob {
  def result := new MutablePrimitiveByteArray(this.length);
  for (def i : 0 .. this.length)
    result[i] := this.data[i];
  result;
}
