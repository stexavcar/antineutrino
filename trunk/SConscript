Import('config root')

import os
from os.path import join, dirname, abspath
import sys
sys.path.append(join(root, 'tools'))
import pollock


spec_dir = join(root, 'etc', 'pollock')
pollock_files = [ join(spec_dir, f) for f in os.listdir(spec_dir) if f.endswith('.pollock') ]
pollock_spec = pollock.make_specificiation(spec_dir)


def process_pollock(target, source, env):
  contents = source[0].get_contents()
  output = open(target[0].rstr(), "wt")
  output.write(pollock_spec.process(source[0].rstr(), contents))
  output.close()


def setup_pollock(env):
  env['BUILDERS']['Pollock'] = Builder(
    action = process_pollock,
    suffix = {
      '.cc': '.pp.cc',
      '.c': '.pp.c',
      '.h': '.pp.h'
    }
  )


def unfold_dependencies(lib, elms):
  for dep in lib.libs:
    if not dep in elms:
      elms.append(dep)
      unfold_dependencies(config.libraries[dep], elms)
  return elms


def ensure_built(env, lib):
  if lib.static_object:
    return
  name = lib.name
  sub_env = env.Clone()
  sub_env.Replace(**lib.get_flags(config))
  files = [env.Pollock(f) for f in lib.get_files(config)]
  headers = [env.Pollock(h) for h in lib.get_headers(config)]
  sub_env.Depends(files + headers, pollock_files)
  all_deps = unfold_dependencies(lib, [])
  static_deps = []
  shared_deps = [] 
  for dep in all_deps:
    ensure_built(env, config.libraries[dep])
    static_deps.append(config.libraries[dep].static_object)
    shared_deps.append(config.libraries[dep].shared_object)
  lib.shared_object = sub_env.SharedLibrary(name, files, LIBS=shared_deps)
  lib.static_object = sub_env.StaticLibrary(name, files + static_deps)
  if lib.shared:
    sub_env.Alias(name, lib.shared_object)
  else:
    sub_env.Alias(name, lib.static_object)


def setup():
  env = Environment(builddir=abspath('.'))
  setup_pollock(env)
  for lib in config.libraries.values():
    ensure_built(env, lib)
  for prog in config.programs.values():
    name = prog.name
    sub_env = env.Clone()
    sub_env.Replace(**prog.get_flags(config))
    files = [env.Pollock(f) for f in prog.get_files(config)]
    headers = [env.Pollock(h) for h in prog.get_headers(config)]
    sub_env.Depends(files + headers, pollock_files)
    all_deps = unfold_dependencies(prog, [])
    for lib in all_deps:
      files.append(config.libraries[lib].static_object)
    obj = sub_env.Program(name, files)
    sub_env.Alias(name, obj)


setup()
