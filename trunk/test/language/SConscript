Import('env build config prefix target')
from os.path import join, walk, abspath, exists
from testutils import PyNeutrinoTestCase, is_selected, relative
from buildutils import read_files_from

executable = relative(config['bin'], str(config[prefix + 'program']))
natives = relative(config['bin'], str(config[prefix + 'natives']))

pyneu_path = join(config['root'], 'tools', 'pyneu')
compiler = join(pyneu_path, 'main.py')
pyneu_files = [ join(pyneu_path, f) for f in read_files_from({ }, pyneu_path, join(pyneu_path, 'pyneu.list')) ]
env['BUILDERS']['CompilePyNeutrinoTest'] = Builder(action = "python " + compiler + " -c " + join(config['src'], 'utils', 'consts.h') + ' ' + join(config['root'], 'lib') + " $SOURCE -o $TARGET")

def add_test(arg, dir, fnames):
  tests = []
  for fname in fnames:
    if fname.endswith('.n') and is_selected(config, fname):
      full_path = join(dir, fname)
      short_name = fname[:-2]
      compiled_name = short_name + '.nc'
      compile = env.CompilePyNeutrinoTest(compiled_name, full_path)
      env.Depends(compile, pyneu_files)
      test_file = relative(config['bin'], join(build, compiled_name))
      test_case = PyNeutrinoTestCase(executable, test_file, [ natives ], target)
      config['tests'].append(test_case)
      config['check-deps'].append(compile)
      config['check-deps'].append(executable)

# We have to manually construct the absolute path because otherwise
# scons screws us over by creating an empty clone of the core folder
# in the build directory and automagically using that.
root = join(config['root'], 'test', 'language')
assert exists(root)
walk(root, add_test, None)
